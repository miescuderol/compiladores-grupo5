%{
package compilador;
import compilador.AnalizadorLexico;
import compilador.Token;
import compilador.Entrada;
import compilador.ElementoPolaca;
import java.util.Vector;
import java.util.Stack;
import java.io.IOException;
import java.util.logging.Level;
import java.util.logging.Logger;
%}

%token  IF  THEN  ELSE  ID  CTE_INT  FOR  PRINT  CADENA  ULONGINT  INTEGER  ASIGN  COMP_IGUAL  COMP_MENOR_IGUAL  COMP_MAYOR_IGUAL  COMP_DISTINTO  BEGIN  END  STRUCT  CTE_ULON
%start programa
%left '-' '+'
%left '*' '/'

%%

programa :
	  bloque_declaracion  {yyout("PROGRAMA.");}
	 | bloque_declaracion bloque_sentencias  {yyout("PROGRAMA.");}
	 | bloque_sentencias  {yyout("PROGRAMA.");}

;

bloque_declaracion: declaracion  {yyout("declaracion.");}
		  | bloque_declaracion  declaracion   {yyout("Bloque de declaraciones.");}
;

bloque_sentencias: conjunto_sentencias {yyout("Bloque de sentencias.");}

;

declaracion : tipo lista_variables ';' {
                                         for (int i=0;i<this.identificadores.size();i++) {
                                             this.identificadores.get(i).setTipo_dato(this.tipo_dato);
                                         }
                                         this.tipo_dato = null;
                                         this.identificadores.clear();
                                       }
            | estructura ID ';' {    // Recupero la tabla de simbolos
                                  TablaSimbolos t = anaLex.getTablaSimbolos();
                                  // Recupero la entrada de la estructura padre
                                  Entrada padre = t.get(((Entrada)val_peek(1).obj).getNombre());
                                  padre.setTipo_dato(Tipo.STRUCT);
                                  int distancia_acumulada = 0;
                                  for (int i=0;i<elemento_estructuras.size();i++) {
                                      Entrada e = t.get(elemento_estructuras.get(i));
                                      t.removeEntrada(e.getNombre());
                                      Entrada eNueva = new EntradaEstructura(e.getNombre(), e.getTipo(), e.getValor(),padre,distancia_acumulada);
                                      eNueva.setTipo_dato(e.getTipo_dato());
                                      t.addNuevaEntrada(eNueva);
                                      // Actualizo la distancia
                                      if (e.getTipo_dato()==Tipo.INTEGER)
                                          distancia_acumulada = distancia_acumulada + 2;
                                      else if (e.getTipo_dato()==Tipo.ULONGINT)
                                          distancia_acumulada = distancia_acumulada + 4;
                                  }}

            | tipo  ';' { yyerror("falta nombre de variable en declaracion.");}
            | estructura ';' {yyerror("falta nombre de la estructura en declaraciï¿½n.");}
            | estructura ID ',' lista_variables ';' {yyerror("declaracion multiple en estructuras esta prohibida.");}
            | lista_variables ';'  { yyerror("declaracion de variable sin tipo definido.");}
            | tipo lista_variables  { yyerror("se esperaba ';'.");}
            | estructura ID { yyerror("se esperaba ';'.");}
;

tipo : INTEGER {this.tipo_dato=Tipo.INTEGER;}
     | ULONGINT {this.tipo_dato=Tipo.ULONGINT;}
     | error {yyerror("tipo primitivo no admitido por el lenguaje.");}
;

estructura : STRUCT {yyout("Sentencia de declaracion de estructura"); elemento_estructuras = new Vector<String>();} '{' cuerpo_estructura '}'
;

cuerpo_estructura : tipo ID ';' {
                                      setearTipoElementosEstructuras((Entrada)$2.obj);
                                }
		  | tipo ID ';' {
                                      setearTipoElementosEstructuras((Entrada)$2.obj);
                                } cuerpo_estructura

                  | tipo ID { yyerror("se esperaba ';'.");}
		  | tipo ID cuerpo_estructura { yyerror("se esperaba ';'.");}
;

lista_variables : ID {this.identificadores.add(anaLex.getTablaSimbolos().get(((Entrada)$1.obj).getNombre()));}
                | ID ',' lista_variables {this.identificadores.add(anaLex.getTablaSimbolos().get(((Entrada)$1.obj).getNombre()));}
;

bloque : sentencia   {yyout("Una sola sentencia.");}
       | BEGIN conjunto_sentencias END {yyout("Bloque de sentencias.");}
       | conjunto_sentencias END { yyerror("se esperaba un 'begin' al inicio de un bloque de sentencias ejecutables.");}
       | BEGIN conjunto_sentencias { yyerror("se esperaba un 'end' al final de un bloque de sentencias ejecutables.");}
;

conjunto_sentencias : sentencia
                    | sentencia conjunto_sentencias
;

sentencia : seleccion
          | bucle
          | impresion
          | asignacion
;


seleccion : IF condicion  {apilar(ElementoPolaca.BF);} seleccionThen  {yyout("Condicional.");}

          | IF error seleccionThen { yyerror("la condicion de seleccion no es valida.");}
;

seleccionThen: THEN bloque {desapilar(1); apilar(ElementoPolaca.BI);agregarRotulo();} seleccionElse

               | error bloque seleccionElse ';' {yyerror("se esperaba un 'then'.");}
               | THEN seleccionElse ';' {yyerror("se esperaba un bloque luego de la palabra reservada 'then'.");}
;

seleccionElse: ELSE bloque ';' {desapilar(0); agregarRotulo();}

               | error bloque ';' {yyerror("se esperaba un 'else'.");}
               | ELSE ';' {yyerror("se esperaba un bloque luego de la palabra reservada 'else'.");}
;

condicion :  '(' expresion '>' expresion ')'  {yyout("CONDICION");agregarAPolaca(ElementoPolaca.MAYOR);verificarTipos($2,$4);}
			| '(' expresion '<' expresion ')'  {yyout("CONDICION");agregarAPolaca(ElementoPolaca.MENOR);verificarTipos($2,$4);}
			| '(' expresion COMP_IGUAL expresion ')'  {yyout("CONDICION");agregarAPolaca(ElementoPolaca.IGUAL);verificarTipos($2,$4);}
			| '(' expresion COMP_DISTINTO expresion ')'  {yyout("CONDICION");agregarAPolaca(ElementoPolaca.DISTINTO);verificarTipos($2,$4);}
			| '(' expresion COMP_MENOR_IGUAL expresion ')'  {yyout("CONDICION");agregarAPolaca(ElementoPolaca.MENORIGUAL);verificarTipos($2,$4);}
			| '(' expresion COMP_MAYOR_IGUAL expresion ')'  {yyout("CONDICION");agregarAPolaca(ElementoPolaca.MAYORIGUAL);verificarTipos($2,$4);}
	
			|'(' expresion  '>'{yyerror("Falta la expresion derecha de la condicion.");} ')' 
			|'(' expresion  '<'{yyerror("Falta la expresion derecha de la condicion.");} ')' 
			|'(' expresion  COMP_IGUAL{yyerror("Falta la expresion derecha de la condicion.");} ')'
			|'(' expresion  COMP_DISTINTO{yyerror("Falta la expresion derecha de la condicion.");} ')'
			|'(' expresion  COMP_MENOR_IGUAL{yyerror("Falta la expresion derecha de la condicion.");} ')'
			|'(' expresion  COMP_MAYOR_IGUAL{yyerror("Falta la expresion derecha de la condicion.");} ')'
			
			|  '(' expresion '>' expresion {yyerror("Falta ')' al final de la condicion.");} 
			|  '(' expresion '<' expresion {yyerror("Falta ')' al final de la condicion.");} 
			|  '(' expresion COMP_IGUAL expresion {yyerror("Falta ')' al final de la condicion.");}
			|  '(' expresion COMP_DISTINTO expresion {yyerror("Falta ')' al final de la condicion.");}
			|  '(' expresion COMP_MENOR_IGUAL expresion {yyerror("Falta ')' al final de la condicion.");}
			|  '(' expresion COMP_MAYOR_IGUAL expresion {yyerror("Falta ')' al final de la condicion.");}

			|'(' error {yyerror("Falta la expresion izquierda de la condicion.");} ')' 
			|  error {yyerror("Falta '(' al comienzo de la condicion.");} ')' 

		;

expresion : expresion '+' termino {agregarAPolaca(ElementoPolaca.SUMA);yyout("Sentencia de suma.");}
          | expresion '-' termino {agregarAPolaca(ElementoPolaca.RESTA);yyout("Sentencia de resta.");}
          | termino

          | expresion '+'  { yyerror("se esperaba un termino luego del '+'.");}
          | expresion '-'   { yyerror("se esperaba un termino luego del '-'.");}

;

asignacion : ID ASIGN expresion ';' {
                                        Entrada e = anaLex.getTablaSimbolos().get(((Entrada)$1.obj).getNombre());
                                        estaDeclarada(e);
                                        agregarAPolacaId((Entrada)$1.obj); 
                                        agregarAPolaca(ElementoPolaca.ASIGNACION);
                                    }
           | elemento_estructura ASIGN expresion ';'    {yyout("Asignacion a elemento de estructura.");}

           | ID  expresion ';'    { yyerror("se esperaba el operador de asignacion ':='.");}
           | ID ASIGN  ';'        { yyerror("se esperaba una expresion del lado derecho de la asignacion.");}
           | ID ASIGN expresion error { yyerror("se esperaba ';'.");}
;

termino : termino '*' factor {agregarAPolaca(ElementoPolaca.MULTIPLICACION);yyout("Sentencia de producto.");}
        | termino '/' factor {agregarAPolaca(ElementoPolaca.DIVISION);yyout("Sentencia de division.");}
        | factor

        | termino '*'   { yyerror("se esperaba un factor luego de '*'.");}
        | termino '/'   { yyerror("se esperaba un factor luego de '/'.");}
;

factor : factor_for
       | elemento_estructura
;

elemento_estructura : ID '.' ID  {yyout("Elemento de estructura.");}

                    | ID ID    { yyerror("se esperaba un punto.");}
                    | ID '.'   { yyerror("se esperaba identificador de la variable mientro de la estructura.");}
                    | '.' ID   { yyerror("se esperaba identificador de la estructura antes del punto.");}
;

bucle :  FOR condicion_for bloque ';'   {yyout("Bucle for.");}

      | FOR condicion_for ';' {yyerror("se esperaba un bloque de sentencias, pero se encontro ';'.");}
      | FOR condicion_for bloque error  { yyerror("se esperaba ';'.");}
;

condicion_for : '(' ID ASIGN factor_for ';' factor_for '>' factor_for ';' factor_for ')' {
                                                                                            Entrada e = anaLex.getTablaSimbolos().get(((Entrada)$2.obj).getNombre());
                                                                                            estaDeclarada(e);
                                                                                         }

              | ID ASIGN factor_for ';' factor_for '>' factor_for ';' factor_for ')'    { yyerror("se esperaba '(' antes de la condiciï¿½n.");}
              | '(' ASIGN factor_for ';' factor_for '>' factor_for ';' factor_for ')'     { yyerror("se esperaba un identificador antes del operador de asignaciï¿½n.");}
              | '(' ID ASIGN ';' factor_for '>' factor_for ';' factor_for ')'     { yyerror("se esperaba un identificador o constante despues del operador de asignaciï¿½n.");}
              | '(' ID ASIGN factor_for ';' '>' factor_for ';' factor_for ')'     { yyerror("se esperaba un identificador o constante antes de '>'.");}
              | '(' ID ASIGN factor_for ';' factor_for '>' ';' factor_for ')'     { yyerror("se esperaba un identificador o constante despues '>'.");}
              | '(' ID ASIGN factor_for ';' factor_for '>' factor_for ';' ')'     { yyerror("se esperaba un identificador o constante despues de ';'.");}

              | '(' ID error factor_for ';' factor_for '>' factor_for ';' factor_for ')'    { yyerror("se esperaba operador de asignaciï¿½n.");}
              | '(' ID ASIGN factor_for error factor_for '>' factor_for ';' factor_for ')'  { yyerror("se esperaba ';'.");}
              | '(' ID ASIGN factor_for ';' factor_for error { yyerror("se esperaba '>'.");} factor ';' factor ')'
              | '(' ID ASIGN factor_for ';' factor_for '>' factor_for error factor_for ')'  { yyerror("se esperaba ';'.");}
              | '(' ID ASIGN factor_for ';' factor_for '>' factor_for ';' factor_for error   { yyerror("se esperaba ')' despues de la condiciï¿½n.");}
              | error {yyerror("error en la sentencia for.");}
;


factor_for : ID {Entrada e = anaLex.getTablaSimbolos().get(((Entrada)$1.obj).getNombre());
                 estaDeclarada(e);
                 agregarAPolacaId((Entrada)$1.obj);}
           | CTE_ULON    {agregarAPolacaConstante((Entrada)$1.obj);}
           | CTE_INT       { anaLex.getTablaSimbolos().get(((Entrada)$1.obj).getNombre()).visitar();
                             fueraRangoIntPositivo((Entrada)$1.obj);
                             agregarAPolacaConstante((Entrada)$1.obj); }
           | '-' CTE_INT   {Entrada actual = anaLex.getTablaSimbolos().get(((Entrada)$2.obj).getNombre());
    				if (!(actual).isVisitado()) {
        			Entrada e = new Entrada("-" + actual.getNombre(), actual.getTipo(), (Integer) actual.getValor() * -1);
        			anaLex.getTablaSimbolos().addNuevaEntrada(e);
        			anaLex.getTablaSimbolos().removeEntrada(((Entrada)($2.obj)).getNombre());
    				} else {
        			Entrada e = new Entrada("-" + actual.getNombre(), actual.getTipo(),(Integer)actual.getValor() * -1);
        			anaLex.getTablaSimbolos().addNuevaEntrada(e);
 				}
 				agregarAPolacaConstante((Entrada)$1.obj);
                                 //tal vez convenga pasar directamente e, que es la nueva entrada con el signo negativo puesto
			    }
;

impresion : PRINT '(' CADENA ')' ';' {yyout("Sentencia de impresion.");
                                      agregarAPolacaConstante((Entrada)$3.obj);
                                      agregarAPolaca(ElementoPolaca.PRINT);}

          | '(' CADENA ')' ';' { yyerror("se esperaba 'print'");}
          | PRINT '(' ')' ';' { yyerror("se esperaba una cadena dentro de la instruccion print.");}
          | PRINT CADENA ')' ';' { yyerror("se esperaba '(' antes de la cadena en sentencia print.");}
          | PRINT '(' CADENA ';' { yyerror("sentencia print sin parentesis de cierre.");}

          | PRINT '(' CADENA ')' error { yyerror("se esperaba ';.'");}
;

%%

private AnalizadorLexico anaLex;
private Vector<String> errores;
private Vector<String> tokens;
private Vector<String> salida;
private Vector<String> elemento_estructuras;
private String error_anterior;
private int linea_error_anterior;

// para manejo de los tipos de datos
private Tipo tipo_dato;
private Vector<Entrada> identificadores = new Vector<Entrada>();

// para manejo de errores semanticos
private Vector<String> erroresSemanticos = new Vector<String>();

//para manejo de polaca inversa
private Vector<ElementoPolaca> polacaInversa = new Vector<ElementoPolaca>();
Stack<Integer> pila=new Stack();
Stack<Integer> retornos=new Stack();

public static final int MAX_INTEGER_NEG = 32767;
public static final int MAX_INTEGER = 32768;
public static final float MAX_ULONGINTEGER = 4294967295f;

public Parser(AnalizadorLexico analLex){
  anaLex=analLex;
  error_anterior = "";
  errores = new Vector<String>();
  tokens = new Vector<String>();
  salida = new Vector<String>();
}

int yylex() {
        Token t = this.anaLex.getToken();
        tokens.add(t.getNombre());
        yylval = t.getAtributoYacc();
        return t.getCodigoYacc();
}

void yyout(String texto) {
  salida.add("En linea " + anaLex.getNumeroLinea() + " " + texto);

}

void yyerror(String texto) {
     if(!texto.equals("syntax error"))
          errores.add("En linea: " + anaLex.getNumeroLinea() + " se encontro el error: " + texto);
     else{
         if(error_anterior.equals(texto))
              errores.add("En linea: " + linea_error_anterior + " ocurrio un error sintactico");
     }
     linea_error_anterior = anaLex.getNumeroLinea();
     error_anterior = texto;
}

public Vector<String> getErrores(){
	return this.errores;
}

public Vector<String> getTokens(){
        return this.tokens;
}

public Vector<String> getSalida(){
   Vector<String> retorno = new Vector<String>();
   retorno.add("Analizador léxico: ");
   retorno.addAll(this.tokens);
   retorno.add("Analizador sintáctico: ");
   retorno.addAll(this.salida);
   return retorno;
}

private void fueraRangoIntPositivo(Entrada e){
    int val = (Integer) (e.getValor());
    if ((0 <= val) && (val > MAX_INTEGER-1))
       yyerror("entero fuera de rango [-32768;32767]");
}

private void setearTipoElementosEstructuras(Entrada e) {
    elemento_estructuras.add(e.getNombre());
    e = anaLex.getTablaSimbolos().get(e.getNombre());
    e.setTipo_dato(this.tipo_dato);
    this.tipo_dato = null;
}

private void estaDeclarada(Entrada e) {
    if (e.getTipo_dato()==null) {
        this.erroresSemanticos.add("La variable " + e.getNombre() + " no está declarada.");
    }
}

public Vector<String> getErroresSemanticos() {
    return this.erroresSemanticos;
}

public Vector<ElementoPolaca> getPolacaInversa(){
    return this.polacaInversa;
}



//metodos para controlar la polaca
void agregarAPolacaId(Entrada e){
    Tipo t = anaLex.getTablaSimbolos().get(e.getNombre()).getTipo_dato();
    System.out.println("tipo de la entrada: " + t);
    if(t == Tipo.INTEGER)
       polacaInversa.add(new ElementoPolaca(ElementoPolaca.VAR_INT,e.getNombre()));
    else
       polacaInversa.add(new ElementoPolaca(ElementoPolaca.VAR_ULONG,e.getNombre()));
}

void agregarAPolacaConstante(Entrada e){
    if(e.getTipo() == Tipo.CONSTANTE_INTEGER)
    polacaInversa.add(new ElementoPolaca(ElementoPolaca.CONS_INT,e.getValor().toString()));
    else
      if(e.getTipo() == Tipo.CONSTANTE_ULONGINT)
        polacaInversa.add(new ElementoPolaca(ElementoPolaca.CONS_ULONG,e.getValor().toString()));
      else
        polacaInversa.add(new ElementoPolaca(ElementoPolaca.CADENA,e.getValor().toString()));
}

void agregarAPolaca(int t){
		  polacaInversa.add(new ElementoPolaca(t));
}

void apilar(int s) {
        pila.push(polacaInversa.size());
        polacaInversa.add(new ElementoPolaca(s));
}

void desapilar(int desplazamiento) {
        int pos = pila.pop().intValue();
        int direccion = polacaInversa.size()+desplazamiento;
        polacaInversa.get(pos).nombre = "LABEL_"+Integer.toString(direccion);
}

void agregarRotulo(){
    polacaInversa.add(new ElementoPolaca(ElementoPolaca.ROTULO,"LABEL_"+Integer.toString(polacaInversa.size())));
}

void apilarDireccion() {
        retornos.push(polacaInversa.size());
}

void desapilarDireccion() {
        int pos=pila.pop().intValue();
        int direccion=retornos.pop();
        polacaInversa.get(pos).nombre="LABEL_"+Integer.toString(direccion);
}